#!/usr/bin/env zsh

#/ DESCRIPTION:
#/   Install Homebrew packages from machine-specific Brewfiles.
#/   Combines Brewfile, Brewfile.optional, Brewfile.{work|personal}, and Brewfile.vsc
#/   into a unified Brewfile.local based on the current machine hostname.
#/
#/ USAGE:
#/   install

setopt ERR_EXIT NO_UNSET PIPE_FAIL

# Source helper functions if not already loaded
typeset -r SCRIPT_DIR="${0:A:h}"
typeset -r DOTFILES_ROOT="${SCRIPT_DIR:h:h}"
if [[ ! $(typeset -f section) ]]; then
  source "${DOTFILES_ROOT}/inc/helpers.zsh"
fi

# Change to script directory to find Brewfiles
cd "$SCRIPT_DIR"

# Machine hostnames (change these to match your machines)
typeset -r WORK_MACHINE_NAME="0x73746f65"
typeset -r PERSONAL_MACHINE_NAME="6x73746f65"
typeset -r BREWFILE_LOCAL="Brewfile.local"

# Cleanup trap to remove temporary Brewfile.local on exit
cleanup_brewfile() {
  [[ -f "$BREWFILE_LOCAL" ]] && rm -f "$BREWFILE_LOCAL" "${BREWFILE_LOCAL}.lock.json"
}
trap cleanup_brewfile EXIT INT TERM

section "homebrew" "üç∫ üíª install"

# Verify required Brewfiles exist
[[ ! -f Brewfile ]] && abort "Brewfile not found" && exit 1
[[ ! -f Brewfile.optional ]] && abort "Brewfile.optional not found" && exit 1

# Get the local computer's hostname
typeset machine_name
machine_name=$(hostname)

# Combine base Brewfiles
typeset brewfile_all
brewfile_all="$(cat Brewfile)\n\n$(cat Brewfile.optional)"

# Load machine-specific Brewfile based on hostname
case "$machine_name" in
  "$WORK_MACHINE_NAME")
    section "homebrew" "üç∫ üíº"
    [[ ! -f Brewfile.work ]] && abort "Brewfile.work not found" && exit 1
    brewfile_all="${brewfile_all}\n\n$(cat Brewfile.work)"
    ;;
  "$PERSONAL_MACHINE_NAME")
    section "homebrew" "üç∫ üè°"
    [[ ! -f Brewfile.personal ]] && abort "Brewfile.personal not found" && exit 1
    brewfile_all="${brewfile_all}\n\n$(cat Brewfile.personal)"
    ;;
  *)
    abort "Unknown machine name: $machine_name"
    exit 1
    ;;
esac

# Add VS Code extensions
[[ ! -f Brewfile.vsc ]] && abort "Brewfile.vsc not found" && exit 1
brewfile_all="${brewfile_all}\n\n$(cat Brewfile.vsc)"

# Write unified Brewfile
echo "$brewfile_all" > "$BREWFILE_LOCAL"

# Execute brew bundle install
typeset cmd="brew bundle --file $BREWFILE_LOCAL"
formatexec "$cmd"

ok "homebrew üç∫ done"
